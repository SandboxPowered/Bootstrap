plugins {
    id 'fabric-loom' version '0.4-SNAPSHOT'
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

def ENV = System.getenv()
group 'org.sandboxpowered'
archivesBaseName = 'bootstrap'
version = "${project.bootstrap_version}.${ENV.BUILD_NUMBER ?: '0-custom'}"

repositories {
    maven {
        name = 'Sandbox Maven'
        url = 'https://maven.sandboxpowered.org'
    }
    maven {
        name = 'Sandbox Bintray'
        url = 'https://dl.bintray.com/sandboxpowered/API'
    }
    jcenter()
}

minecraft {
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_mappings}"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    compileOnly "org.jetbrains:annotations:19.0.0"
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

bintray {
    user = ENV.BINTRAY_USER
    key = ENV.BINTRAY_KEY
    publications = ['maven']
    publish = true

    pkg {
        repo = 'Loader'
        name = 'bootstrap'
        userOrg = 'sandboxpowered'
        licenses = ['LGPL-3.0']
        vcsUrl = 'https://github.com/sandboxpowered/bootstrap.git'
        publicDownloadNumbers = true
        githubRepo = 'sandboxpowered/bootstrap'
        githubReleaseNotesFile = 'README.md'
        version {
            name = project.version
            desc = ''
            vcsTag = project.version
            released  = new Date()
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact remapJar
        }
    }
}
